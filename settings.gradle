pluginManagement {
	includeBuild 'gradle/plugins/nokeebuild-plugins'
}

plugins {
	id 'dev.gradleplugins.gradle-plugin-development' version '1.7.1'
}

include 'elements'
include 'gradle-fixtures-source-elements'
include 'nokeetemplates-plugins'
include 'source-elements-annotation-processor'
include 'test-gradle-projects'

gradle.allprojects {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}

	pluginManager.withPlugin('java-base') {
		java {
			sourceCompatibility = JavaVersion.VERSION_1_8
			targetCompatibility = JavaVersion.VERSION_1_8
		}
	}
	apply plugin: 'maven-publish'
}

gradle.rootProject {
	tasks.register('check') { dependsOn(subprojects.collect { "${it.name}:check" }) }
}

sourceControl {
	gitRepository(uri("https://github.com/nokeedev/nokee-hamcrest"))
	gitRepository(uri("https://github.com/gradle-plugins/gradle-build-script")) {
		producesModule('dev.gradleplugins:gradle-build-script')
	}
}
